buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.fablap.defib"
        minSdkVersion 11
        targetSdkVersion 22
        versionCode 3
        versionName "0.3"
    }

    signingConfigs {
        releaseSigning {
            // Keystore and alias are in ~/.gradle/gradle.properties
            def console = System.console()
            if (console != null) {
                storePassword System.console().readLine("\nStore password: ")
            }
            storeFile file("${android_keystore}")
            keyAlias "${android_key_alias}"
            if (console != null) {
                keyPassword System.console().readLine("\nKey password: ")
            }
        }
    }

    buildTypes {

        debug {
            buildConfigField 'boolean', 'IS_CRASHLYTICS_ENABLED', 'false'
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        staging {
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'IS_CRASHLYTICS_ENABLED', 'true'
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
        }

        release {
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.releaseSigning
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'IS_CRASHLYTICS_ENABLED', 'true'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:22.0.+'
    compile 'com.android.support:appcompat-v7:22.0.+'
    compile 'com.android.support:support-annotations:21.0.+'
    compile 'com.google.android.gms:play-services-location:7.0.+'
    compile 'com.google.android.gms:play-services-maps:7.0.+'
    compile 'com.google.maps.android:android-maps-utils:0.3+'
    compile 'uk.co.chrisjenx:calligraphy:2.0.+'
    compile 'com.melnykov:floatingactionbutton:1.2.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.avast:android-styled-dialogs:2.2.+'
    compile 'com.nispok:snackbar:2.10.+'
    compile 'com.github.amlcurran.showcaseview:library:5.0.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.2@aar') {
        transitive = true;
    }
}
